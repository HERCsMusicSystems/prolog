[import "http"]
[import "store"]

;//======== LOAD DATABASE =========//

[batch "capitol.txt"]

;//======== CONTROLLERS ===========//

[[controller_index *req *res]
	[*res "HTTP/1.1 200 OK\n\n<body>\n"]
	[view_list_all *res]
	[*res "</body>"]
]

[[controller_delete *req *res *country]
	[*res "HTTP/1.1 200 OK\n\n<body>\n"]
	[*res "DELETING " *country "<br/><br/>\n"]
	[TRY [delcl [[capitol *country *]]]]
	[view_list_all *res]
	[*res "</body>"]
]

[[controller_insert *req *res]
	[*req param "country" *country]
	[*req param "city" *city]
	[*res "HTTP/1.1 200 OK\n\n<body>\n"]
	[*res "INSERTING " *country " " *city "<br/><br/>\n"]
	[addcl [[capitol *country *city]]]
	[view_list_all *res]
	[*res "</body>"]
]

[[controller_persist *req *res]
	[*res "HTTP/1.1 200 OK\n\n<body>\n"]
	[*res "STORED<br/><br/>\n"]
	[store_database "capitol.txt" capitol]
	[view_list_all *res]
	[*res "</body>"]
]

[[controller_call *req *res *command]
	[session_header *req *res *session "Content-Type: application/json"]
	[list *req]
	[show *command]
	[*req HTTP_SEARCH *c]
	[show [*session *c]]
	[*session *c *reply]
	[show *reply]
	[json *reply *json]
	[*res *json]
]

;//========== VIEW(s) =========//

[[view_list_all *res]
	[FORM *res POST capitol insert] [*res "\n"]
	[*res "<table>\n"]
	[*res "<tr><td><h1>COUNTRY</h1></td><td><h1>CAPITOL</h1></td></tr>\n"]
	[ALL
		[capitol *country *city]
		[*res "<tr><td>" *country "</td><td>" *city "</td>"]
		[*res "<td>"] [GET *res capitol delete *country delete] [*res "</td></tr>\n"]
	]
	[*res "<tr><td>"] [param *res country] [*res "</td><td>"] [param *res city] [*res "</td><td>"] [param *res submit button NEW] [*res "</td></tr>\n"]
	[*res "</table>\n"]
	[FORM *res]
	[*res "\n"]
	[GET *res capitol persist "Save all changes permanently."]
]

;//========== ROUTER ==========//

[[router *req *res]
	[*req HTTP_URI : *route]
	[show [route *req *res : *route]]
	[route *req *res : *route]
]

;//========== ROUTES ===========//

[[route *req *res * call : *command] [controller_call *req *res *command]]
[[route *req *res GET capitol] [controller_index *req *res]]
[[route *req *res GET capitol delete *country] [controller_delete *req *res *country]]
[[route *req *res GET capitol persist] [controller_persist *req *res]]
[[route *req *res POST capitol insert] [controller_insert *req *res]]
[[route *req *res GET ""] [header *res *atom] [hrcs *res] [*res "<body><h1>Prolog Webserver</h1></body>"]]
[[route *req *res GET *file_name] [*res [] : *file_name]]
[[route *req *res : *] [*res "HTTP/1.1 401 Not found\n\n"]]

;//========== WEBSERVER ===========//

[webserver service 3008 router]
[show "ready"]
[command]

;//========== END-OF-SCRIPT ===========//
[exit]

